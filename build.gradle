apply from: rootProject.file('dependencies.gradle')

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "dev.gradleplugins:gradle-plugin-development:0.0.21"
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'java-gradle-plugin'
    apply plugin: "com.gradle.plugin-publish"

    apply from: rootProject.file('dependencies.gradle')

    project.group = 'io.logbee'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        testCompile(dependency.spock) {
            exclude group: 'org.codehaus.groovy'
        }
    }

    sourceSets {
        integrationTest {
            groovy {
                srcDir file('src/integration/groovy')
            }
            resources {
                srcDir file('src/integration/resources')
            }
            compileClasspath += sourceSets.main.output + configurations.testRuntime
            runtimeClasspath += output + compileClasspath
        }
    }

    task integrationTest(type: Test) {
        description = 'Runs the integration tests.'
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        mustRunAfter test
    }

    check.dependsOn integrationTest
}